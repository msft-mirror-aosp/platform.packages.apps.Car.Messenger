/*
 * Copyright (C) 2021 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'jacoco'

android {
    compileSdkVersion gradle.ext.aaosLatestSDK

    defaultConfig {
        applicationId "com.android.car.messenger"
        minSdkVersion 31
        targetSdkVersion gradle.ext.aaosTargetSDK
        testInstrumentationRunnerArguments clearPackageData: 'true', coverage: 'true'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        debug {
            testCoverageEnabled true
        }
    }

    project.gradle.taskGraph.whenReady {
        connectedFakeDebugAndroidTest {
            ignoreFailures = true
        }
    }

    flavorDimensions 'devMode'
    productFlavors {
        prod {
            dimension 'devMode'
        }

        fake {
            dimension 'devMode'
            versionNameSuffix '-fake'
            testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
        }

        androidTestFake {
            setRoot('tests')
            java.srcDirs = ['tests/src']
        }
    }

    testOptions {
        animationsDisabled true
    }

    // Disable release build for fake build
    variantFilter { variant ->
        def names = variant.flavors*.name
        if (variant.buildType.name == 'release') {
            if (names.contains('fake')) {
                setIgnore(true)
            }
        }
    }

    signingConfigs {
        debug {
            storeFile file('../libs/certs/com_android_car_apps_test.jks')
            storePassword 'carapps'
            keyAlias 'carapps'
            keyPassword 'carapps'
        }
    }
}

dependencies {
    implementation files(gradle.ext.lib_car_system_stubs)

    implementation "androidx.constraintlayout:constraintlayout:2.1.0"

    def lifecycle_version = "2.2.0"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    // Not available in 2.3+
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"

    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "androidx.preference:preference:1.1.1"
    implementation "androidx.annotation:annotation:1.0.1"

    implementation project(':car-apps-common')
    implementation project(":car-assist-lib")
    implementation project(":car-messenger-common:model")
    implementation project(":car-ui-lib")
    implementation project(':car-apps-common')

    fakeImplementation 'com.linkedin.dexmaker:dexmaker-mockito-inline-extended:2.28.1'
    fakeImplementation project(path: ":car-messenger-app:testing")

    androidTestImplementation('androidx.test.espresso:espresso-core:3.4.0') {
        exclude group: 'org.checkerframework', module: 'checker'
    }
    androidTestImplementation('androidx.test.espresso:espresso-contrib:3.4.0') {
        exclude group: 'org.checkerframework', module: 'checker'
    }
    androidTestImplementation('androidx.test.espresso:espresso-intents:3.4.0') {
        exclude group: 'org.checkerframework', module: 'checker'
    }
    androidTestImplementation 'org.hamcrest:hamcrest:2.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test:core:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'com.google.truth:truth:1.1.3'
    androidTestImplementation 'com.linkedin.dexmaker:dexmaker-mockito-inline-extended:2.28.1'
}
